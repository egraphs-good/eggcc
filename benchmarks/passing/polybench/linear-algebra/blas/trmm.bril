## trmm computes a triangular matrix multiplication

@main {
    # constants
    # dimensions correspond to Polybench MEDIUM_DATASET

    M: int = const 200;
    fM: float = const 200;
    N: int = const 240;
    fN: float = const 240;

    one: int = const 1;
    fzero: float = const 0;

    # initialize arrays
    A: ptr<float> = call @matrix_new M M;
    B: ptr<float> = call @matrix_new M N;
    alpha: float = const 1.5;
    call @init A B M fM N fN;
    # main computation

    i: int = const 0;
.main_i:
    cond: bool = lt i M;
    br cond .main_i_body .main_i_done;
.main_i_body:
    j: int = const 0;
.main_j:
    cond: bool = lt j N;
    br cond .main_j_body .main_j_done;
.main_j_body:
    k: int = add i one;
.main_k:
    cond: bool = lt k M;
    br cond .main_k_body .main_k_done;
.main_k_body:
    Aki: float = call @matrix_get A k i M;
    Bkj: float = call @matrix_get B k j N;
    incr: float = fmul Aki Bkj;
    call @matrix_incr B i j N incr;
    k: int = add k one;
    jmp .main_k;
.main_k_done:
    Bij: float = call @matrix_get B i j N;
    new_Bij: float = fmul alpha Bij;
    call @matrix_set B i j N new_Bij;
    j: int = add j one;
    jmp .main_j;
.main_j_done:
    i: int = add i one;
    jmp .main_i;
.main_i_done:

    res: float = call @matrix_sum B M N;

    free A;
    free B;
    print res;
}

@init(A: ptr<float>, B: ptr<float>,
      M: int, fM: float, N: int, fN: float) {
    one: int = const 1;
    fone: float = const 1;

    i: int = const 0;
    fi: float = const 0;
.init_i:
    cond: bool = lt i M;
    br cond .init_i_body .init_i_done;
.init_i_body:
    j: int = const 0;
    fj: float = const 0;
.init_A_j:
    cond: bool = lt j i;
    br cond .init_A_j_body .init_A_j_done;
.init_A_j_body:
    val: float = fadd fi fj;
    val: float = call @fmod val fM;
    val: float = fdiv val fM;
    call @matrix_set A i j M val;

    j: int = add j one;
    fj: float = fadd fj fone;
    jmp .init_A_j;
.init_A_j_done:
    call @matrix_set A i i M fone;
    j: int = const 0;
    fj: float = const 0;
.init_B_j:
    cond: bool = lt j N;
    br cond .init_B_j_body .init_B_j_done;
.init_B_j_body:
    val: float = fsub fi fj;
    val: float = fadd fN val;
    val: float = call @fmod val fN;
    val: float = fdiv val fN;
    call @matrix_set B i j N val;

    j: int = add j one;
    fj: float = fadd fj fone;
    jmp .init_B_j;
.init_B_j_done:

    i: int = add i one;
    fi: float = fadd fi fone;
    jmp .init_i;
.init_i_done:
}

@matrix_new(Nrow: int, Ncol: int): ptr<float> {
    total: int = mul Nrow Ncol;
    ptr: ptr<float> = alloc total;
    ret ptr;
}

@matrix_loc(mtx: ptr<float>, row: int, col: int, Ncol: int): ptr<float> {
    row_offset: int = mul row Ncol;
    offset: int = add row_offset col;
    new_ptr: ptr<float> = ptradd mtx offset;
    ret new_ptr;
}

# EXPECTS:
#   @matrix_loc defined
@matrix_get(mtx: ptr<float>, row: int, col: int, Ncol: int): float {
    ptr: ptr<float> = call @matrix_loc mtx row col Ncol;
    val: float = load ptr;
    ret val;
}

# EXPECTS:
#   @matrix_loc defined
@matrix_set(mtx: ptr<float>, row: int, col: int, Ncol: int, val: float) {
    ptr: ptr<float> = call @matrix_loc mtx row col Ncol;
    store ptr val;
}

# EXPECTS:
#   @matrix_loc defined
@matrix_incr(mtx: ptr<float>, row: int, col: int, Ncol: int, incr: float) {
    ptr: ptr<float> = call @matrix_loc mtx row col Ncol;
    val: float = load ptr;
    new_val: float = fadd val incr;
    store ptr new_val;
}

@matrix_sum(mtx: ptr<float>, Nrow: int, Ncol: int): float {
    i: int = const 0;
    one: int = const 1;
    total: int = mul Nrow Ncol;
    res: float = const 0;
.while:
    cond: bool = lt i total;
    br cond .body .done;
.body:
    mtx_loc: ptr<float> = ptradd mtx i;
    val: float = load mtx_loc;
    res: float = fadd res val;
    i: int = add i one;
    jmp .while;
.done:
    ret res;
}


# Search for n % m where n and m are floats by
# iteratively subtracting the largest m*2^k that
# fits inside n. 
# Takes O((log n/m)^2) time.
# NOTE: In C, this can be done with a built in 
# function, but this is the best we can do.
@fmod(n: float, m: float): float {
    zero: float = const 0;
    two: float = const 2;
    rem: float = id n;
.while:
    cond: bool = fge rem m;
    br cond .body .done;
.body:
    decr: float = id m;
.while_inner:
    diff: float = fsub rem decr;
    cond: bool = fge diff zero;
    br cond .body_inner .done_inner;
.body_inner:
    decr: float = fmul decr two;
    jmp .while_inner;
.done_inner:
    decr: float = fdiv decr two;
    rem: float = fsub rem decr;
    jmp .while;
.done:
    ret rem;
}