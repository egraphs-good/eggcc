# ARGS: 3500000
# An implementation of Quicksort using the Lomuto partition scheme, adapted from the pseudocode on Wikipedia
# Input: an array of length 6
# Output: the input array sorted in ascending order
# Adopted two helper functions, pack and print_array, from Jiajie Li's bubble sort benchmark

@main(loop_bound: int) {
  loop_incr: int = const 1;
  loop_counter: int = const 10;
  final_output: int = const 0;
.loop_cond:
  loop_cond: bool = lt loop_counter loop_bound;
  br loop_cond .loop_body .loop_done;
.loop_body:
  output: int = call @orig_main loop_counter;
  final_output: int = add final_output output;
  loop_counter: int = add loop_counter loop_incr;
  jmp .loop_cond;
.loop_done:
  print final_output;
}

@orig_main(x: int): int {
    size: int = const 6;
    ninetyfour: int = const 94;
    n1: int = add x ninetyfour;
    n2: int = const 21;
    n3: int = const 5;
    n4: int = const 6;
    n5: int = const 82;
    n6: int = const 46;
    array: ptr<int> = call @pack size n1 n2 n3 n4 n5 n6;
    one: int = const 1;
    size_minus_one: int = sub size one;

    zero: int = const 0;
    call @qsort array zero size_minus_one;

    # Print array
    sum: int = call @print_array array size;

    free array;
    ret sum;
}

@qsort(array : ptr<int>, l: int, r:int) {
    # if l >= r or r < 0, return
    l_ge_r: bool = ge l r;
    zero: int = const 0;
    neg_r: bool = lt r zero;
    ret_cond: bool = or l_ge_r neg_r;

    br ret_cond .done .continue; 

    .continue:
        p: int = call @partition array l r;
        one: int = const 1;
        p_minus_one: int = sub p one;
        p_plus_one: int = add p one;

        call @qsort array l p_minus_one;
        call @qsort array p_plus_one r;

    .done:
        ret;
}

@partition(array : ptr<int>, l: int, r:int) : int {
    # choose the last element as the pivot
    pivot_loc: ptr<int> = ptradd array r;
    pivot: int = load pivot_loc;
    one: int = const 1;
    i:int = sub l one;
    j:int = id i;

    .loop.init:
        j:int = add j one;
        cond: bool = lt j r;
        br cond .body .post.loop;

    .body:
        j_loc: ptr<int> = ptradd array j;
        a_j: int = load j_loc;
        swap_cond: bool = le a_j pivot;
        br swap_cond .swap .loop.init;

    .swap:
        # increment i by 1
        i: int = add i one;

        # swap the current element with the element at the temporary pivot index i
        i_loc: ptr<int> = ptradd array i;
        a_i: int = load i_loc;
        store j_loc a_i;
        store i_loc a_j;
        jmp .loop.init;

    .post.loop:
        i:int = add i one;
        i_loc: ptr<int> = ptradd array i;
        a_i: int = load i_loc;
        store i_loc pivot;
        store pivot_loc a_i;
        ret i;
}

# allocate an array of the given size and pack the input values into the array
@pack(size: int, n1: int, n2: int, n3: int, n4: int, n5: int, n6: int) : ptr<int> {
    one: int = const 1;
    i: int = const 0;
    array: ptr<int> = alloc size;
# Pack data into array manually. Cannot use loop because of the different var name.     
    loc: ptr<int> = ptradd array i;
    store loc n1;
    i: int = add i one;
    loc: ptr<int> = ptradd array i;
    store loc n2;
    i: int = add i one;        
    loc: ptr<int> = ptradd array i;
    store loc n3;
    i: int = add i one;        
    loc: ptr<int> = ptradd array i;
    store loc n4;
    i: int = add i one;        
    loc: ptr<int> = ptradd array i;
    store loc n5;
    i: int = add i one;        
    loc: ptr<int> = ptradd array i;
    store loc n6;

    ret array;
}

@print_array(array: ptr<int>, size: int): int {
    sum: int = const 0;
    i: int = const 0;
    one: int = const 1;
.loop:
    cond: bool = lt i size;
    br cond .body .done;
.body:
    loc: ptr<int> = ptradd array i;
    val: int = load loc;
    val: int = mul val i;
    sum: int = add sum val;
.loop_end:
    i: int = add i one;
    jmp .loop;
.done:
    ret sum;
}