# A standard binary search, not super interesting
# Inputs: An array of 5 elements (fixed), and a target
# Output: Index of target, or -1 if not found
# Adopted pack and print_array code from bubblesort by Jiajie Li

@pack(size: int, n1: int, n2: int, n3: int, n4: int, n5: int) : ptr<int> {
    one: int = const 1;
    i: int = const 0;
    array: ptr<int> = alloc size;
# Pack data into array manually. Cannot use loop because of the different var name.
    loc: ptr<int> = ptradd array i;
    store loc n1;
    i: int = add i one;
    loc: ptr<int> = ptradd array i;
    store loc n2;
    i: int = add i one;
    loc: ptr<int> = ptradd array i;
    store loc n3;
    i: int = add i one;
    loc: ptr<int> = ptradd array i;
    store loc n4;
    i: int = add i one;
    loc: ptr<int> = ptradd array i;
    store loc n5;
    ret array;
}

@print_array(array: ptr<int>, size: int) {
    i: int = const 0;
    one: int = const 1;
.loop:
    cond: bool = lt i size;
    br cond .body .done;
.body:
    loc: ptr<int> = ptradd array i;
    val: int = load loc;
    print val;
.loop_end:
    i: int = add i one;
    jmp .loop;
.done:
    ret;
}

# The function that does the actual computation
# Left and Right are the search range

@binary_search(array: ptr<int>, target: int, left: int, right: int): int {
    negone: int = const -1;
    zero: int = const 0;
    one: int = const 1;
    two: int = const 2;
.check_valid:
    end_cond: bool = gt left right;
    br end_cond .end .body;
.body:
    mid: int = add left right;
    mid: int = div mid two;
.check_equal:
    midloc: ptr<int> = ptradd array mid;
    midval: int = load midloc;
    equal_cond: bool = eq target midval;
    br equal_cond .found .check_gt;
.found:
    ret mid;
.check_gt:
    gt_cond: bool = gt target midval;
    br gt_cond .call_gt .call_lt;
.call_gt:
    newleft: int = add left one;
    output: int = call @binary_search array target newleft right;
    ret output;
.call_lt:
    newright: int = sub right one;
    output: int = call @binary_search array target left newright;
    ret output;
.end:
    output: int = id negone;
    ret output;
}

@orig_main(e1: int, e2: int, e3: int, e4: int, e5: int) {
    target: int = const 7;
    size: int = const 5;
    zero: int = const 0;
    four: int = const 4;
    array: ptr<int> = call @pack size e1 e2 e3 e4 e5;
    output: int = call @binary_search array target zero four;
    print output;
    free array;
}

@main() {
  loop_incr: int = const 1;
  loop_counter: int = const 10;
  loop_bound: int = const 25;
.loop_cond:
  loop_cond: bool = lt loop_counter loop_bound;
  br loop_cond .loop_body .loop_done;
.loop_body:
  loop2_counter: int = const 10;
.loop2_cond:
  loop2_cond: bool = lt loop2_counter loop_bound;
  br loop2_cond .loop2_body .loop2_done;
.loop2_body:
  loop3_counter: int = const 10;
.loop3_cond:
  loop3_cond: bool = lt loop3_counter loop_bound;
  br loop3_cond .loop3_body .loop3_done;
.loop3_body:
  loop4_counter: int = const 10;
.loop4_cond:
  loop4_cond: bool = lt loop4_counter loop_bound;
  br loop4_cond .loop4_body .loop4_done;
.loop4_body:
  loop5_counter: int = const 10;
.loop5_cond:
  loop5_cond: bool = lt loop5_counter loop_bound;
  br loop5_cond .loop5_body .loop5_done;
.loop5_body:
  call @orig_main loop_counter loop2_counter loop3_counter loop4_counter loop5_counter;
  loop5_counter: int = add loop5_counter loop_incr;
  jmp .loop5_cond;
.loop5_done:  
  loop4_counter: int = add loop4_counter loop_incr;
  jmp .loop4_cond;
.loop4_done:  
  loop3_counter: int = add loop3_counter loop_incr;
  jmp .loop3_cond;
.loop3_done:  
  loop2_counter: int = add loop2_counter loop_incr;
  jmp .loop2_cond;
.loop2_done:
  loop_counter: int = add loop_counter loop_incr;
  jmp .loop_cond;
.loop_done:
}